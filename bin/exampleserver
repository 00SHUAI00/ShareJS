#!/usr/bin/env coffee

connect = require 'connect'
sharejs = require '../src/server'
hat = require('hat').rack(32, 36)

argv = require('optimist').
	usage("Usage: $0 [-p portnum]").
	default('p', 8000).
	alias('p', 'port').
	argv

server = connect(
	connect.favicon(),
	connect.static(__dirname + '/../examples'),
	connect.router (app) ->

		renderer = require '../examples/_static'
		app.get '/static/:docName', (req, res, next) ->
			docName = req.params.docName
			renderer docName, server.model, res, next

		wiki = require '../examples/_wiki'
		app.get '/wiki/?', (req, res, next) ->
			res.writeHead 301, {location: '/wiki/Main'}
			res.end()

		app.get '/wiki/:docName', (req, res, next) ->
			docName = req.params.docName
			wiki docName, server.model, res, next

		app.get '/pad/?', (req, res, next) ->
			docName = hat()
			res.writeHead 303, {location: '/pad/pad.html#' + docName}
			res.write ''
			res.end()

		app.get '/?', (req, res, next) ->
			res.writeHead 302, {location: '/index.html'}
			res.end()

)

options =
	db:
		type:'memory'
	auth: (client, action) ->
		# This auth handler rejects any ops bound for docs starting with 'readonly'.
		if action.name == 'submit op' and action.docName.match /^readonly/
			action.reject()
		else
			action.accept()

# Lets try and enable redis persistance if redis is installed...
try
	require 'redis'
	options.db = type:'redis'

console.log "Options: ", options

port = argv.p

# Attach the sharejs REST and Socket.io interfaces to the server
sharejs.attach server, options

server.listen port
console.log "Demos running at http://localhost:#{port}/"
